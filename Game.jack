class Game 
{
    field int score; 
    field int delay;

    field int headX, headY;
    field int foodX, foodY;

    field int maxWidth;
    field int maxHeight;
    
    field Snake snake;
    field Food food;

    constructor Game new(int d, int maxW, int maxH)
    {
        let maxWidth = maxW;
        let maxHeight = maxH;
        let snake = Snake.new(5, 2, maxWidth, maxHeight);
        let food = Food.new(1, 200, 150, maxWidth, maxHeight);
        
        let foodX = 200;
        let foodY = 150;

        let score = 0;
        let delay = d;

        do setHead();
    
        return this;
    }
    
    // aqui de fato o jogo acontece
    method void gameLoop()
    {
        var int key;
        var int loop;

        let loop = 0;
        
        while (~(key = 140))
        {
            let key = Keyboard.keyPressed();

            do snake.setDirection(key);
            
            do updateScreen();
            
            do snake.nextPosition();
            do setHead();
            
            if (collidedWithItself())
            {
                do end();
            }

            if (collidedWithFood())
            {
                do snake.growSnake();
                let score = score + food.getPoints();
                do setFood(loop);
            }

            let loop = loop + 1;
            do delay();

        }

        do end();
        return;
    }

    // inicializa o jogo
    method void start()
    {
        do snake.drawSnake(true);
        do gameLoop();
        return;
    }


    // finaliza o jogo
    method void end()
    {
        do Sys.halt();
        return;
    }

    // delay do jogo
    method void delay()
    {
        do Sys.wait(delay);
        return;
    }

    // checa a colisão entre a cabeça da cobra e a comida
    method boolean collidedWithFood()
    {
        var boolean r;
        
        let r = detectCollision(headX, headY, 8, foodX, foodY, 8);
        
        return r;

    }

    // checa colisão da cobra com ela mesma
    method boolean collidedWithItself()
    {
        var boolean r;
        var int i, j;
        var int x, y;
        var Array snakeX, snakeY;

        let i = 0;
        let j = snake.getLength();

        let snakeX = snake.getSnakeX();
        let snakeY = snake.getSnakeY();

        while (i < j)
        {
            let x = snakeX[i];
            let y = snakeY[i];
            let r = detectCollision(headX, headY, 8, x, y, 8);
            
            if (r)
            {
                return true;
            }

            let i = i + 1;
        }
        return false;
    }

    // checa a colisão entre dois retângulos
    method boolean detectCollision(int firstRectX, int firstRectY, int firstRectWidth, int secondRectX, int secondRectY, int secondRectWidth)
    {
        var int t;
        
        let t = secondRectX + secondRectWidth;
        if (firstRectX < t)
        {
            let t = firstRectX + firstRectWidth;
            if (secondRectX < t)
            {
                let t = secondRectY + secondRectWidth;
                if (firstRectY < t)
                {
                    let t = firstRectY + firstRectWidth;
                    if (secondRectY < t)
                    {
                        return true;
                    }
                }
            }
        }
        return false;
    }


    // atualiza o score
    method void updateScreen()
    {
        do Screen.clearScreen();
        do Output.moveCursor(0, 0);
        do Output.printString("Score: ");
        do Output.printInt(score);
        do food.drawFood(true);
        return;
    }

    // atualiza a variável do jogo da cabeça da cobra (posição)
    method void setHead()
    {
        let headX = snake.getHeadX();
        let headY = snake.getHeadY();
        return;
    }

    // atualiza a variável do jogo da comida (posição)
    method void setFood(int r)
    {
        do food.drawFood(false);
        do setRandomX(r);
        do setRandomY(r);
        do food.setFoodX(foodX);
        do food.setFoodY(foodY);
        do food.drawFood(true);
        return;
    }

    // pega o x da comida 
    method void setRandomX(int r)
    {
        var int i;
        let i = r;
        let i = mod(i, maxWidth - 8);
        let foodX = i;
        return;
    }

    // pega o y da comida
    method void setRandomY(int r)
    {
        var int i;
        let i = r;
        let i = mod(i, maxHeight - 8);
        let foodY = i;
        return;
    }

    // resto da divisão (inteiros positivos)
    method int mod(int a, int b)
    {
        var int i;
        let i = a / b;
        let i = b * i;
        let i = a - i;
        return i;
    }
}